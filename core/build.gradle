allprojects {
    apply(plugin: 'java')
    apply(plugin: 'idea')
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
}

def projectName = "YARG (Yet another report generator)"
def projectDescription = "Open source library for report generation. Provided by Haulmont."
def projectUrl = "https://github.com/cuba-platform/yarg"

def artifactVersion = '1.1-SNAPSHOT'
if (rootProject.hasProperty('buildVersion')) {
    artifactVersion = rootProject['buildVersion']
}
def isSnapshot = artifactVersion.endsWith('-SNAPSHOT')

def api = project(':yarg-api')
def core = project(':yarg')

def repositoryUser = System.getenv('HAULMONT_REPOSITORY_USER')
def repositoryPassword, repositoryUrl, workRepositoryUrl;

if (repositoryUser) {//for internal users
    repositoryPassword = System.getenv('HAULMONT_REPOSITORY_PASSWORD')
    repositoryUrl = 'http://repository.haulmont.com:8587/nexus/content'
    workRepositoryUrl = "$repositoryUrl/groups/work"
} else {//for external users
    repositoryUser = 'cuba'
    repositoryPassword = 'cuba123'
    repositoryUrl = 'https://repo.cuba-platform.com/content'
    workRepositoryUrl = "$repositoryUrl/groups/work"
}

configure([api, core]) {
    apply(plugin: 'maven')
    apply(plugin: 'signing')

    group = 'com.haulmont.yarg'
    version = artifactVersion

    repositories {
        mavenCentral()
        maven {
            credentials {
                username repositoryUser
                password repositoryPassword
            }
            url workRepositoryUrl
        }
    }

    sourceSets {
        main {
            java { srcDir 'src' }
            resources { srcDir 'src' }
        }

        test {
            java { srcDir 'test' }
            resources { srcDir 'test' }
        }
    }

    configurations {
        deployerJars
        sourceArchives
    }

    dependencies {
        compile(group: 'com.google.guava', name: 'guava', version: '17.0')
        deployerJars(group: 'org.apache.maven.wagon', name: 'wagon-http', version: '1.0-beta-2')
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.java
        classifier = 'sources'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourceJar
        archives javadocJar
    }

    if (signing.hasProperty('keyId')) {
        signing {
            sign configurations.archives
        }
    }

    uploadArchives.configure {
        repositories.mavenDeployer {
            name = 'httpDeployer'
            configuration = configurations.deployerJars

//            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
//
//            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
//                authentication(userName: "$oss_login", password:  "$oss_password")
//            }

            repository(url: "$repositoryUrl/repositories/" + (isSnapshot ? 'snapshots' : 'releases')) {
                authentication(userName: repositoryUser, password: repositoryPassword)
            }

            pom.project {
                name = "$projectName"
                description = "$projectDescription"
                url = "$projectUrl"
                developers {
                    developer {
                        id 'tinhol'
                        email 'tinhol@mail.ru'
                        organization = 'Haulmont'
                        organizationUrl 'http://www.haulmont.com/'
                    }
                }
                scm {
                    connection = "$projectUrl"
                    url = "$projectUrl"
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }

    install.configure {
        repositories.mavenInstaller {
            pom.project {
                name = "$projectName"
                description = "$projectDescription"
                url = "$projectUrl"
                developers {
                    developer {
                        id 'tinhol'
                        email 'tinhol@mail.ru'
                        organization = 'Haulmont'
                        organizationUrl 'http://www.haulmont.com/'
                    }
                }
                scm {
                    connection = "$projectUrl"
                    url = "$projectUrl"
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

configure(api) {

}

configure(core) {
    apply(plugin: 'application')

    assemble {
        new File("result").mkdir();
        new File("result/integration").mkdir();
        new File("result/smoke").mkdir();
        new File("result/sample").mkdir();
        new File("result/console").mkdir();
    }

    dependencies {
        compile(api)
        compile(group: 'net.openhft', name: 'compiler', version: '2.3.0')
        compile(group: 'commons-cli', name: 'commons-cli', version: '1.2')
        compile(group: 'commons-logging', name: 'commons-logging', version: '1.1.1')
        compile(group: 'commons-lang', name: 'commons-lang', version: '2.6')
        compile(group: 'commons-collections', name: 'commons-collections', version: '3.2.1')
        compile(group: 'commons-io', name: 'commons-io', version: '1.4')
        compile(group: 'commons-dbcp', name: 'commons-dbcp', version: '1.4')
        compile(group: 'dom4j', name: 'dom4j', version: '1.6.1')
        compile(group: 'log4j', name: 'log4j', version: '1.2.16')
        compile(group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5')
        compile(group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.5')
        compile(group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.1.6')
        compile(group: 'antlr', name: 'antlr', version: '2.7.7')
        compile(group: 'asm', name: 'asm', version: '3.3.1')
        compile(group: 'org.antlr', name: 'antlr-runtime', version: '3.2')
        compile(group: 'org.freemarker', name: 'freemarker', version: '2.3.16')
        compile(group: 'org.openoffice', name: 'juh', version: '3.0.0')
        compile(group: 'org.openoffice', name: 'ridl', version: '3.0.0')
        compile(group: 'org.openoffice', name: 'unoil', version: '3.0.0')
        compile(group: "org.apache.avalon.framework", name: "avalon-framework-api", version: '4.3.1')
        compile(group: "org.apache.avalon.framework", name: "avalon-framework-impl", version: '4.3.1')
        compile(group: "org.apache.xmlgraphics", name: "fop", version: "1.0")
        compile(group: "org.apache.xmlgraphics", name: "xmlgraphics-commons", version: "1.5")
        compile(group: "org.plutext", name: "jaxb-svg11", version: "1.0.2")
        compile(group: "org.plutext", name: "jaxb-xslfo", version: "1.0.1")
        compile(group: "org.plutext", name: "jaxb-xmldsig-core", version: "1.0.0")
        compile(group: "org.apache.xmlgraphics", name: "batik-bridge", version: "1.7")
        compile(group: "org.apache.xmlgraphics", name: "batik-svggen", version: "1.7")
        compile(group: "org.apache.xmlgraphics", name: "batik-awt-util", version: "1.7")
        compile(group: "org.apache.xmlgraphics", name: "batik-css", version: "1.7")
        compile(group: "xalan", name: "xalan", version: "2.7.1")
        compile(group: "xalan", name: "serializer", version: "2.7.1")
        compile(group: "com.lowagie", name: "itext", version: "2.1.7") {
            exclude(group: 'org.bouncycastle', module: 'bctsp-jdk14')
            exclude(group: 'org.bouncycastle', module: 'bcprov-jdk14')
            exclude(group: 'org.bouncycastle', module: 'bcmail-jdk14')
            exclude(group: 'bouncycastle', module: 'bcmail-jdk14')
            exclude(group: 'bouncycastle', module: 'bcprov-jdk14')
            exclude(group: 'bouncycastle', module: 'bctsp-jdk14')
        }
        compile(group: 'org.apache.poi', name: 'poi', version: '3.15')
        compile(group: 'org.apache.poi', name: 'poi-scratchpad', version: '3.9') {
            exclude(group: 'org.apache.poi', module: 'poi')
        }
        compile(group: 'commons-codec', name: 'commons-codec', version: '1.6')
        compile(group: 'org.xhtmlrenderer', name: 'flying-saucer-core', version: '9.0.6')
        compile(group: 'org.xhtmlrenderer', name: 'flying-saucer-pdf', version: '9.0.6') {
            exclude(group: 'org.bouncycastle', module: 'bctsp-jdk14')
            exclude(group: 'org.bouncycastle', module: 'bcprov-jdk14')
            exclude(group: 'org.bouncycastle', module: 'bcmail-jdk14')
            exclude(group: 'bouncycastle', module: 'bcmail-jdk14')
            exclude(group: 'bouncycastle', module: 'bcprov-jdk14')
            exclude(group: 'bouncycastle', module: 'bctsp-jdk14')
        }
        compile(group: "org.docx4j", name: "docx4j", version: "3.3.3") {
            exclude(group: 'org.apache.poi', module: 'poi')
            exclude(group: 'junit', module: 'junit')
        }
        compile(group: "org.docx4j", name: "docx4j-ImportXHTML", version: "3.3.1") {
            exclude(group: 'junit', module: 'junit')
            exclude(group: 'org.apache.poi', module: 'poi')
        }
        compile(group: "org.docx4j", name: "docx4j-export-fo", version: "3.3.1") {
            exclude(group: 'junit', module: 'junit')
        }
        compile(group: "com.jayway.jsonpath", name: "json-path", version: "2.1.0")
        compile(group: 'org.xerial', name: 'sqlite-jdbc', version: '3.18.0')
        compile files('/home/newink/web/src/lib/ojdbc7.jar')


        testCompile(group: 'junit', name: 'junit', version: '4.5')
        testCompile(group: 'org.hsqldb', name: 'hsqldb', version: '2.3.3')

    }

    mainClassName = "com.haulmont.yarg.console.ConsoleRunner"

    configurations.archives.artifacts.removeAll {
        it.file =~ 'tar' || it.file =~ 'zip'
    }

    startScripts {
        doLast {
            copy {
                from "reporting.properties"
                into outputDir
            }
        }
    }

    installDist {
        doLast {
            def outputDir = destinationDir
            copy {
                from "reporting.properties"
                into "$outputDir/bin"
            }
        }
    }

    test {
        maxHeapSize = "512m"
        jvmArgs '-XX:MaxPermSize=256m'
        workingDir = new File('../..')
    }
}